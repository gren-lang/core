module Test.Json exposing (tests)

import Basics exposing (..)
import Expect
import Json.Decode as Json
import Result exposing (..)
import String
import Test exposing (..)


tests : Test
tests =
    describe "Json decode"
        [ intTests
        , customTests
        , andMapTests
        ]


intTests : Test
intTests =
    let
        testInt val str =
            when Json.decodeString Json.int str is
                Ok _ ->
                    Expect.equal val True

                Err _ ->
                    Expect.equal val False
    in
    describe "Json decode int"
        [ test "whole int" <| \{} -> testInt True "4"
        , test "-whole int" <| \{} -> testInt True "-4"
        , test "whole float" <| \{} -> testInt True "4.0"
        , test "-whole float" <| \{} -> testInt True "-4.0"
        , test "large int" <| \{} -> testInt True "1801439850948"
        , test "-large int" <| \{} -> testInt True "-1801439850948"
        , test "float" <| \{} -> testInt False "4.2"
        , test "-float" <| \{} -> testInt False "-4.2"
        , test "Infinity" <| \{} -> testInt False "Infinity"
        , test "-Infinity" <| \{} -> testInt False "-Infinity"
        , test "NaN" <| \{} -> testInt False "NaN"
        , test "-NaN" <| \{} -> testInt False "-NaN"
        , test "true" <| \{} -> testInt False "true"
        , test "false" <| \{} -> testInt False "false"
        , test "string" <| \{} -> testInt False "\"string\""
        , test "object" <| \{} -> testInt False "{}"
        , test "null" <| \{} -> testInt False "null"
        , test "undefined" <| \{} -> testInt False "undefined"
        , test "Decoder expects object finds array, was crashing runtime." <|
            \{} ->
                Expect.equal
                    (Err "Problem with the given value:\n\n[]\n\nExpecting an OBJECT")
                    (Result.mapError Json.errorToString (Json.decodeString (Json.dict Json.float) "[]"))
        ]


customTests : Test
customTests =
    let
        jsonString =
            """
            { "foo": "bar" }
            """

        customErrorMessage =
            "I want to see this message!"

        myDecoder =
            Json.field "foo" Json.string |> Json.andThen (\_ -> Json.fail customErrorMessage)

        assertion =
            when Json.decodeString myDecoder jsonString is
                Ok _ ->
                    Expect.fail "expected `customDecoder` to produce a value of type Err, but got Ok"

                Err message ->
                    if String.contains customErrorMessage (Json.errorToString message) then
                        Expect.pass

                    else
                        Expect.fail <|
                            "expected `customDecoder` to preserve user's error message '"
                                ++ customErrorMessage
                                ++ "', but instead got: "
                                ++ Json.errorToString message
    in
    test "customDecoder preserves user error messages" <| \{} -> assertion

-- Used for testing andMap
type alias TestRecord1 =
    { stringField : String
    , intField : Int
    , boolField: Bool
    , floatField: Float
    , stringArrayField: Array String
    }

-- The constructor for TestRecord1
mkTestRecord1: String -> Int -> Bool -> Float -> Array String -> TestRecord1
mkTestRecord1 s i b f sa =
    { stringField = s
    , intField = i
    , boolField = b
    , floatField = f
    , stringArrayField = sa
    }

andMapTests : Test
andMapTests =
    let
        jsonString =
            """
            {
                "stringField": "bar",
                "intField": 42,
                "boolField": true,
                "floatField": 1.5,
                "stringArrayField": [ "a", "b" ]
            }
            """

        myDecoder =
            Json.succeed mkTestRecord1
                |> Json.andMap (Json.field "stringField" Json.string)
                |> Json.andMap (Json.field "intField" Json.int)
                |> Json.andMap (Json.field "boolField" Json.bool)
                |> Json.andMap (Json.field "floatField" Json.float)
                |> Json.andMap (Json.field "stringArrayField" (Json.array Json.string))

        parse = Json.decodeString myDecoder jsonString

        record = when parse is
            Ok r -> r
            Err _ -> mkTestRecord1 "" 0 False 0.0 []

    in
        describe "Json andMap "
            [test "string field" <|
                \{} -> Expect.equal record.stringField "bar"
            , test "int field" <|
                \{} -> Expect.equal record.intField 42
            , test "bool field" <|
                \{} -> Expect.equal record.boolField True
            , test "float field" <|
                \{} -> Expect.within (Expect.Relative 0.001) record.floatField 1.5
            , test "string-array field" <|
                \{} -> Expect.equalArrays record.stringArrayField [ "a", "b" ]
            , test "parse error" <|
                \{} -> Expect.ok parse

            ]
